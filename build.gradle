buildscript {
  repositories {
    jcenter()
  }
  
  dependencies {
    classpath 'com.google.cloud.tools:appengine-gradle-plugin:+'
  }
}

plugins {
  id 'java'
  id 'java-library'
  id 'war'
  id 'com.eriwen.gradle.css' version '2.14.0'
  id 'eu.butter.gradle.js' version '2.15.1'
}

apply plugin: 'com.google.cloud.tools.appengine-appyaml'


repositories {
  jcenter()
}

dependencies {
  providedCompile 'org.eclipse.jetty:jetty-server:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-webapp:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-util:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:jetty-annotations:9.4.30.v20200611'
  providedCompile 'org.eclipse.jetty:apache-jsp:9.4.30.v20200611'
  
  implementation 'jstl:jstl:1.2'
  implementation 'org.mifmi:mifmi-commons4j:0.2.7'
  api 'commons-codec:commons-codec:1.14'
  api 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
  
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.+'
}

appengine {
  deploy {
    projectId 'GCLOUD_CONFIG'
    version 'GCLOUD_CONFIG'
    promote false
    stopPreviousVersion false
  }
}

combineCss {
  source "${webAppDirName}/static/css/main.css"
  dest "${buildDir}/minify/css/all.css"
}

minifyCss {
  source combineCss
  dest "${buildDir}/minify/css/all.min.css"
}

combineJs {
  encoding 'UTF-8'
  source "${webAppDirName}/static/js/analytics.js"
  source "${webAppDirName}/static/js/core.js"
  source "${webAppDirName}/static/js/main.js"
  source "${webAppDirName}/static/js/adsense.js"
  dest "${buildDir}/minify/js/all.js"
}

minifyJs {
  source combineJs
  dest "${buildDir}/minify/js/all.min.js"
  sourceMap "${buildDir}/minify/js/all.sourcemap.json"
  closure {
    compilerOptions.languageOut = 'ECMASCRIPT5'
  }
}

war {
  dependsOn minifyCss, minifyJs
  
  webInf {
    from "${buildDir}/minify/css/"
    into "/static/css/"
  }
  
  webInf {
    from "${buildDir}/minify/js/"
    into "/static/js/"
  }
}

jar {
  manifest {
    attributes 'Main-Class': 'com.dencode.web.server.ServerMain'
  }
}

task copyJar(type: Copy) {
  dependsOn jar
  
  from project.libsDir
  include '*.jar'
  into appengine.stage.stagingDirectory
}

task copyDependencies(type: Copy) {
  from configurations.providedCompile
  into appengine.stage.stagingDirectory
}

appengineStage.finalizedBy copyJar, copyDependencies

task appRunStage(type: JavaExec) {
  dependsOn appengineStage
  
  environment PORT: 8080
  workingDir appengine.stage.stagingDirectory
  classpath "${appengine.stage.stagingDirectory}/*"
  main 'com.dencode.web.server.ServerMain'
}

group = 'com.dencode'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 11
targetCompatibility = 11
